{1- Se organiz칩 una competencia entre el equipo rojo y el equipo azul. Cada equipo consta de dos robots, y debe realizar una tarea: - Los robots R1 y R2 del equipo rojo debe juntar todas las flores de las avenidas 2 y 3 respectivamente - Los robots A1 y A2 del equipo azul debe juntar todos los papeles de las calles 98 y 99 respectivamente Al finalizar la competencia, un robot fiscalizador deber치 informar el equipo que junt칩 m치s objetos.}
programa ejercicio1P5
procesos
  {__________________________________}
  proceso asignarID
  comenzar
    EnviarMensaje(1,R1)
    EnviarMensaje(2,R2)
    EnviarMensaje(3,A1)
    EnviarMensaje(4,A2)
  fin
  {__________________________________}
  proceso calcularCantidades(ES cantFlores: numero; ES cantPapeles: numero)
  variables
    ID,cF,cP: numero
  comenzar
    RecibirMensaje(ID,*)
    si(ID = 1)
      RecibirMensaje(cF,R1)
      cantFlores := cantFlores + cF
    sino
      si(ID = 2)
        RecibirMensaje(cF,R2)
        cantFlores := cantFlores + cF
      sino
        si(ID = 3)
          RecibirMensaje(cP,A1)
          cantPapeles := cantPapeles + cP
        sino
          RecibirMensaje(cP,A2)
          cantPapeles := cantPapeles + cP 
  fin
  {__________________________________}
  proceso informarGanadror(E cantFlores: numero; E cantPapeles: numero)
  comenzar
    Informar('Flores',cantFlores)
    Informar('Papeles',cantPapeles)
    si(cantFlores > cantPapeles)
      Informar('Ganador',1)
    sino
      si(cantPapeles > cantFlores)
        Informar('Ganador',2)
      sino
        Informar('Empate',0)
  fin
  {__________________________________}
  proceso recogerFlores(ES cantFlores: numero)
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
      cantFlores := cantFlores + 1
  fin
  {__________________________________}
  proceso recogerPapeles(ES cantPapeles: numero)
  comenzar
    mientras(HayPapelEnLaEsquina)
      tomarPapel
      cantPapeles := cantPapeles + 1
  fin
  {__________________________________}
  proceso esqEnConflictoF(ES cantFlores: numero)
  variables
    avBloq,caBloq: numero
  comenzar
    recogerFlores(cantFlores)
    avBloq := PosAv
    caBloq := PosCa + 1
    BloquearEsquina(avBloq,caBloq)
    mover
    recogerFlores(cantFlores)
    BloquearEsquina(avBloq,caBloq + 1)
    mover
    LiberarEsquina(avBloq,caBloq)
    recogerFlores(cantFlores)
    mover
    LiberarEsquina(avBloq,caBloq + 1)
    recogerFlores(cantFlores)
  fin
  {__________________________________}
  proceso recorrerAvenidaF(ES cantFlores: numero)
  comenzar
    repetir 96
      recogerFlores(cantFlores)
      mover
    esqEnConflictoF(cantFlores)
  fin
  {__________________________________}
  proceso esqEnConflictoP(ES cantPapeles: numero)
  variables
    avBloq,caBloq: numero
  comenzar
    recogerPapeles(cantPapeles)
    avBloq := PosAv + 1
    caBloq := PosCa
    BloquearEsquina(avBloq,caBloq)
    mover
    recogerPapeles(cantPapeles)
    BloquearEsquina(avBloq+1,caBloq)
    mover
    LiberarEsquina(avBloq,caBloq)
    recogerPapeles(cantPapeles)
    mover
    LiberarEsquina(avBloq+1,caBloq)
  fin
  {__________________________________}
  proceso recorrerAvenidaP(ES cantPapeles: numero)
  comenzar
    esqEnConflictoP(cantPapeles)
    repetir 96
      recogerPapeles(cantPapeles)
      mover
    recogerPapeles(cantPapeles) 
  fin
  {__________________________________}
areas
  areaJefe: AreaP(1,1,1,1)
  cuadrante: AreaPC(2,98,3,99)
  areaR1: AreaP(2,1,2,97)
  areaR1P2: AreaP(2,100,2,100)
  areaR2: AreaP(3,1,3,97)
  areaR2P2: AreaP(3,100,3,100)
  areaA1: AreaP(1,98,1,98)
  areaA1P2: AreaP(4,98,100,98)
  areaA2: AreaP(1,99,1,99)
  areaA2P2: AreaP(4,99,100,99)
robots
  robot robot1
  variables
    cantFlores,cantPapeles: numero
  comenzar
    cantFlores := 0
    cantPapeles := 0
    asignarID
    repetir 4
      calcularCantidades(cantFlores,cantPapeles)
    informarGanadror(cantFlores,cantPapeles)
  fin
  {__________________________________}
  robot robot2
  variables
    cantFlores,ID: numero
  comenzar
    RecibirMensaje(ID,jefe)
    cantFlores := 0
    recorrerAvenidaF(cantFlores)
    EnviarMensaje(ID,jefe)
    EnviarMensaje(cantFlores,jefe)
  fin
  {__________________________________}
  robot robot3
  variables
    cantPapeles,ID: numero
  comenzar
    RecibirMensaje(ID,jefe)
    derecha
    cantPapeles := 0
    recorrerAvenidaP(cantPapeles)
    EnviarMensaje(ID,jefe)
    EnviarMensaje(cantPapeles,jefe)
  fin
  {__________________________________}
variables
  jefe: robot1
  R1: robot2
  R2: robot2
  A1: robot3
  A2: robot3
comenzar
  AsignarArea(jefe,areaJefe)
  AsignarArea(R1,areaR1)
  AsignarArea(R1,areaR1P2)
  AsignarArea(R1,cuadrante)
  AsignarArea(R2,areaR2)
  AsignarArea(R2,areaR2P2)
  AsignarArea(R2,cuadrante)
  AsignarArea(A1,areaA1)
  AsignarArea(A1,areaA1P2)
  AsignarArea(A1,cuadrante)
  AsignarArea(A2,areaA2)
  AsignarArea(A2,areaA2P2)
  AsignarArea(A2,cuadrante)
  Iniciar(jefe,1,1)
  Iniciar(R1,2,1)
  Iniciar(R2,3,1)
  Iniciar(A1,1,98)
  Iniciar(A2,1,99)
fin